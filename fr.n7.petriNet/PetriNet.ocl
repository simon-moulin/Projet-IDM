import 'PetriNet.ecore'

package petrinet

context PetriElement
def: petrinet(): PetriNet =
	PetriNet.allInstances()
		->select(n | n.petriElements -> includes(self))
		->asSequence()->first()
	
context PetriNet
inv validPetriNetName('Invalid PetriNet name: ' + self.name):
	self.name.matches('[A-Za-z_][A-Za-z0-9_]*')
	
context Arc
inv validArcPosition('An arc cannot connect two Places or Two transitions, it can only connect Place-Transition pairs'):
	(self.source.oclIsKindOf(Place)
	 and self.target.oclIsKindOf(Transition))
	 or (self.target.oclIsKindOf(Place)
	 and self.source.oclIsKindOf(Transition))
	 
context Arc
inv hasPositiveWeight('Arc weight should be positive (>= 0)'):
	self.weight >= 0
	
context Arc
inv sourceAndTargetInSamePetriNet('Arc source and target should belong to the same PetriNet'):
	self.source.petrinet() = self.petrinet() and self.target.petrinet() = self.petrinet()
	
context Arc
inv noRedundant('Two Arcs with the same source and the same target should not exist'):
	self.petrinet().petriElements
	->select(e | e.oclIsKindOf(Arc))
	->collect(e | e.oclAsType(Arc))
	->select(e | self.source = e.source and self.target = e.target)
	->size() = 1

context Arc
inv arcFromTransitionIsNotReadArc('An Arc from a Transition cannot be a readArc'):
	not (self.source.oclIsKindOf(Transition) and self.isReadArc)	

context Place
inv hasPositiveMarking('Place marking should be positive: ' + self.name):
	self.marking >= 0
	
context Noeud
inv validNoeudName('Invalid Noeud name: ' + self.name):
	self.name.matches('[A-Za-z_][A-Za-z0-9_]*')
	
context Noeud
inv uniqueNoeudName('Noeud name ' + self.name + ' already exists'):
	self.petrinet().petriElements
		->select(e | e.oclIsKindOf(Noeud))
		->collect(e | e.oclAsType(Noeud))
		->select(e | self.name = e.name)
		->size() = 1

endpackage