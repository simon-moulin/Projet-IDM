grammar fr.n7.simplepdl.txt.PDL with org.eclipse.xtext.common.Terminals

import "http://simplepdl" 
import "http://www.eclipse.org/emf/2002/Ecore" as ecore


Process : 'process' name=ID '{'
    processElements+=ProcessElement*
'}' ;

ProcessElement : WorkDefinition | WorkSequence | Guidance | Resource;

enum WorkSequenceType : startToStart = 'startToStart' 
    | finishToStart = 'finishToStart'
    | startToFinish = 'startToFinish'
    | finishToFinish = 'finishToFinish';

WorkDefinition : 'wd' name=ID '{'
    (need+=Need)*
'}';

Guidance: 'note' text=STRING ('for' elements+=[ProcessElement] ( "," elements+=[ProcessElement])* )?;
    
WorkSequence : 'ws'
	linkType=WorkSequenceType 
	'from' predecessor=[WorkDefinition] 
	'to' successor=[WorkDefinition];
    
Need : 'need' nbResources=INT 'of' resource=[Resource];
    
Resource : 'create' nbAvailableResources=INT 'of' name=ID;